#
# Copyright (c) 2019 The Aquarium Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

import("third_party/dawn/scripts/dawn_features.gni")

if (is_mac) {
  import("build/config/mac/mac_sdk.gni")
}

declare_args() {
  enable_dawn = is_win || (is_linux && !is_chromeos) || is_mac
  enable_angle = false
  enable_d3d12 = is_win
  enable_opengl = is_win || (is_linux && !is_chromeos) || is_mac
}

# RapidJSON is used by both Aquarium and ANGLE tests, so the ideal path
# should be //third_party/rapidjson. However, ANGLE cannot be configured
# to use that path.
rapidjson_dir = "//third_party/angle/third_party/rapidjson"

executable("aquarium") {
  libs = []

  if (is_mac) {
    frameworks = []
    weak_frameworks = []
  }

  sources = [
    "src/aquarium-optimized/Aquarium.cpp",
    "src/aquarium-optimized/Aquarium.h",
    "src/aquarium-optimized/Behavior.cpp",
    "src/aquarium-optimized/Behavior.h",
    "src/aquarium-optimized/Buffer.h",
    "src/aquarium-optimized/BufferManager.cpp",
    "src/aquarium-optimized/BufferManager.h",
    "src/aquarium-optimized/Context.cpp",
    "src/aquarium-optimized/Context.h",
    "src/aquarium-optimized/ContextFactory.cpp",
    "src/aquarium-optimized/ContextFactory.h",
    "src/aquarium-optimized/FishModel.cpp",
    "src/aquarium-optimized/FishModel.h",
    "src/aquarium-optimized/Main.cpp",
    "src/aquarium-optimized/Matrix.h",
    "src/aquarium-optimized/Model.cpp",
    "src/aquarium-optimized/Model.h",
    "src/aquarium-optimized/Program.cpp",
    "src/aquarium-optimized/Program.h",
    "src/aquarium-optimized/ResourceHelper.cpp",
    "src/aquarium-optimized/ResourceHelper.h",
    "src/aquarium-optimized/SeaweedModel.h",
    "src/aquarium-optimized/Texture.cpp",
    "src/aquarium-optimized/Texture.h",
    "src/common/AQUARIUM_ASSERT.h",
    "src/common/FPSTimer.cpp",
    "src/common/FPSTimer.h",
    "src/common/Path.cpp",
    "src/common/Path.h",
  ]

  deps = [
    "$rapidjson_dir:rapidjson",
    "third_party:glfw",
    "third_party:imgui",
    "third_party:stb",
  ]

  include_dirs = [
    "third_party/stb",
    "src",
    "third_party/imgui",
    "third_party/imgui/examples",
    "third_party/glfw/include",
    "third_party/cxxopts/include",
  ]

  defines = [
    "CXXOPTS_NO_EXCEPTIONS",
    "CXXOPTS_NO_RTTI",
  ]
  if (is_win) {
    defines += [ "GLFW_EXPOSE_NATIVE_WIN32" ]
  }
  if (is_linux && !is_chromeos) {
    defines += [ "GLFW_EXPOSE_NATIVE_X11" ]
  }
  if (is_mac) {
    defines += [ "GLFW_EXPOSE_NATIVE_COCOA" ]
  }

  ldflags = []

  if (is_mac) {
    cflags_objcc = []
  }

  if (enable_angle || enable_opengl) {
    sources += [
      "src/aquarium-optimized/opengl/BufferGL.cpp",
      "src/aquarium-optimized/opengl/BufferGL.h",
      "src/aquarium-optimized/opengl/ContextGL.cpp",
      "src/aquarium-optimized/opengl/ContextGL.h",
      "src/aquarium-optimized/opengl/FishModelGL.cpp",
      "src/aquarium-optimized/opengl/FishModelGL.h",
      "src/aquarium-optimized/opengl/GenericModelGL.cpp",
      "src/aquarium-optimized/opengl/GenericModelGL.h",
      "src/aquarium-optimized/opengl/InnerModelGL.cpp",
      "src/aquarium-optimized/opengl/InnerModelGL.h",
      "src/aquarium-optimized/opengl/OutsideModelGL.cpp",
      "src/aquarium-optimized/opengl/OutsideModelGL.h",
      "src/aquarium-optimized/opengl/ProgramGL.cpp",
      "src/aquarium-optimized/opengl/ProgramGL.h",
      "src/aquarium-optimized/opengl/SeaweedModelGL.cpp",
      "src/aquarium-optimized/opengl/SeaweedModelGL.h",
      "src/aquarium-optimized/opengl/TextureGL.cpp",
      "src/aquarium-optimized/opengl/TextureGL.h",
      "src/aquarium-optimized/opengl/imgui_impl_opengl3.cpp",
      "src/aquarium-optimized/opengl/imgui_impl_opengl3.h",
    ]
  }

  if (enable_angle) {
    enable_dawn = false
    enable_d3d12 = false

    defines += [
      "ENABLE_ANGLE_BACKEND",
      "GL_GLES_PROTOTYPES",
      "EGL_EGL_PROTOTYPES",
      "GL_GLEXT_PROTOTYPES",
      "IMGUI_IMPL_OPENGL_LOADER_CUSTOM",
      "IMGUI_IMPL_OPENGL_ES3",
    ]

    include_dirs += [ "third_party/angle/include" ]

    deps += [
      "third_party/angle:libANGLE",
      "third_party/angle:libEGL",
      "third_party/angle:libGLESv2",
    ]
  } else {
    deps += [ "third_party:glad" ]

    include_dirs += [ "third_party/glad/include" ]
    defines += [ "IMGUI_IMPL_OPENGL_LOADER_GLAD" ]
  }

  if (enable_dawn) {
    defines += [
      "ENABLE_DAWN_BACKEND",
      "DAWN_SKIP_ASSERT_SHORTHANDS",
    ]
    if (dawn_enable_d3d12) {
      defines += [ "DAWN_ENABLE_BACKEND_D3D12" ]
    }
    if (dawn_enable_metal) {
      defines += [ "DAWN_ENABLE_BACKEND_METAL" ]
    }
    if (dawn_enable_vulkan) {
      defines += [ "DAWN_ENABLE_BACKEND_VULKAN" ]
    }

    include_dirs += [ "third_party/dawn/src" ]

    if (dawn_enable_metal) {
      if (is_mac) {
        frameworks += [
          "AppKit.framework",
          "QuartzCore.framework",
        ]
        if (mac_min_system_version == "10.10.0") {
          weak_frameworks += [ "Metal.framework" ]
        } else {
          frameworks += [ "Metal.framework" ]
        }
      }
    }
    if (dawn_enable_vulkan) {
      if (is_linux && !is_chromeos) {
        libs += [ "dl" ]
      }
    }

    sources += [
      "src/aquarium-optimized/dawn/BufferDawn.cpp",
      "src/aquarium-optimized/dawn/BufferDawn.h",
      "src/aquarium-optimized/dawn/BufferManagerDawn.cpp",
      "src/aquarium-optimized/dawn/BufferManagerDawn.h",
      "src/aquarium-optimized/dawn/ContextDawn.cpp",
      "src/aquarium-optimized/dawn/ContextDawn.h",
      "src/aquarium-optimized/dawn/FishModelDawn.cpp",
      "src/aquarium-optimized/dawn/FishModelDawn.h",
      "src/aquarium-optimized/dawn/FishModelInstancedDrawDawn.cpp",
      "src/aquarium-optimized/dawn/FishModelInstancedDrawDawn.h",
      "src/aquarium-optimized/dawn/GenericModelDawn.cpp",
      "src/aquarium-optimized/dawn/GenericModelDawn.h",
      "src/aquarium-optimized/dawn/InnerModelDawn.cpp",
      "src/aquarium-optimized/dawn/InnerModelDawn.h",
      "src/aquarium-optimized/dawn/OutsideModelDawn.cpp",
      "src/aquarium-optimized/dawn/OutsideModelDawn.h",
      "src/aquarium-optimized/dawn/PlatformContextDawn.h",
      "src/aquarium-optimized/dawn/ProgramDawn.cpp",
      "src/aquarium-optimized/dawn/ProgramDawn.h",
      "src/aquarium-optimized/dawn/SeaweedModelDawn.cpp",
      "src/aquarium-optimized/dawn/SeaweedModelDawn.h",
      "src/aquarium-optimized/dawn/TextureDawn.cpp",
      "src/aquarium-optimized/dawn/TextureDawn.h",
      "src/aquarium-optimized/dawn/imgui_impl_dawn.cpp",
      "src/aquarium-optimized/dawn/imgui_impl_dawn.h",
    ]
    if (dawn_enable_metal) {
      sources += [ "src/aquarium-optimized/dawn/PlatformContextDawn.mm" ]
    } else {
      sources += [ "src/aquarium-optimized/dawn/PlatformContextDawn.cpp" ]
    }

    deps += [
      # "third_party/SPIRV-Tools",
      "third_party/dawn/src/dawn:dawn_headers",
      "third_party/dawn/src/dawn:dawn_proc",
      "third_party/dawn/src/dawn:dawncpp",
      "third_party/dawn/src/dawn_native",
      "third_party/glslang:glslang_sources",
    ]
    if (dawn_enable_vulkan) {
      deps += [ "third_party/dawn/third_party/khronos:vulkan_headers" ]
    }

    if (dawn_enable_metal) {
      if (is_mac) {
        if (mac_deployment_target == "10.10.0") {
          cflags_objcc += [ "-Wno-unguarded-availability" ]
        }
      }
    }
  }

  if (enable_d3d12) {
    defines += [ "ENABLE_D3D12_BACKEND" ]

    include_dirs += [ "src/include/d3d12" ]

    libs += [
      "d3d12.lib",
      "dxgi.lib",
      "d3dcompiler.lib",
    ]

    sources += [
      "src/aquarium-optimized/d3d12/BufferD3D12.cpp",
      "src/aquarium-optimized/d3d12/BufferD3D12.h",
      "src/aquarium-optimized/d3d12/ContextD3D12.cpp",
      "src/aquarium-optimized/d3d12/ContextD3D12.h",
      "src/aquarium-optimized/d3d12/FishModelD3D12.cpp",
      "src/aquarium-optimized/d3d12/FishModelD3D12.h",
      "src/aquarium-optimized/d3d12/FishModelInstancedDrawD3D12.cpp",
      "src/aquarium-optimized/d3d12/FishModelInstancedDrawD3D12.h",
      "src/aquarium-optimized/d3d12/GenericModelD3D12.cpp",
      "src/aquarium-optimized/d3d12/GenericModelD3D12.h",
      "src/aquarium-optimized/d3d12/InnerModelD3D12.cpp",
      "src/aquarium-optimized/d3d12/InnerModelD3D12.h",
      "src/aquarium-optimized/d3d12/OutsideModelD3D12.cpp",
      "src/aquarium-optimized/d3d12/OutsideModelD3D12.h",
      "src/aquarium-optimized/d3d12/ProgramD3D12.cpp",
      "src/aquarium-optimized/d3d12/ProgramD3D12.h",
      "src/aquarium-optimized/d3d12/SeaweedModelD3D12.cpp",
      "src/aquarium-optimized/d3d12/SeaweedModelD3D12.h",
      "src/aquarium-optimized/d3d12/TextureD3D12.cpp",
      "src/aquarium-optimized/d3d12/TextureD3D12.h",
      "src/aquarium-optimized/d3d12/imgui_impl_dx12.cpp",
      "src/aquarium-optimized/d3d12/imgui_impl_dx12.h",
    ]
  }

  if (enable_opengl) {
    defines += [ "ENABLE_OPENGL_BACKEND" ]
  }

  cflags_cc = [
    "-Wno-string-conversion",
    "-Wno-unused-result",
    "-Wno-format-security",
    "-Wno-microsoft-enum-forward-reference",
  ]
}

executable("aquarium-direct-map") {
  sources = [
    "src/aquarium-direct-map/AttribBuffer.cpp",
    "src/aquarium-direct-map/AttribBuffer.h",
    "src/aquarium-direct-map/Buffer.cpp",
    "src/aquarium-direct-map/Buffer.h",
    "src/aquarium-direct-map/Globals.h",
    "src/aquarium-direct-map/Main.cpp",
    "src/aquarium-direct-map/Matrix.h",
    "src/aquarium-direct-map/Model.cpp",
    "src/aquarium-direct-map/Model.h",
    "src/aquarium-direct-map/Program.cpp",
    "src/aquarium-direct-map/Program.h",
    "src/aquarium-direct-map/Scene.cpp",
    "src/aquarium-direct-map/Scene.h",
    "src/aquarium-direct-map/Texture.cpp",
    "src/aquarium-direct-map/Texture.h",
    "src/aquarium-direct-map/Uniform.cpp",
    "src/aquarium-direct-map/Uniform.h",
    "src/common/AQUARIUM_ASSERT.h",
    "src/common/FPSTimer.cpp",
    "src/common/FPSTimer.h",
    "src/common/Path.cpp",
    "src/common/Path.h",
  ]

  deps = [
    "$rapidjson_dir:rapidjson",
    "third_party:glad",
    "third_party:glfw",
    "third_party:stb",
  ]

  include_dirs = [
    "third_party/glad/include",
    "third_party/stb",
    "third_party/glfw/include",
    "src",
  ]

  cflags_cc = [
    "-Wno-string-conversion",
    "-Wno-unused-result",
  ]
}
